/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2014-2022 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'groovy'
    id 'org.kordamp.gradle.java-project'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

config {
    info {
        name          = rootProject.name
        description   = 'Additional transformers for the maven-shade-plugin'
        inceptionYear = '2014'
        tags          = ['maven', 'shade']

        specification  { enabled = false }
    }

    docs {
        javadoc {
            autoLinks {
                enabled = false
            }
        }
    }

    coverage {
        jacoco {
            toolVersion = jacocoVersion
        }
    }

    publishing {
        signing = true
    }

    bintray {
        enabled = false
    }
}

allprojects {
    apply plugin: 'idea'
}

idea {
    project {
        jdkName project.sourceCompatibility
        languageLevel project.sourceCompatibility

        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

dependencies {
    compileOnly        'org.apache.maven.plugins:maven-shade-plugin:3.2.4'
    testCompileOnly    'org.apache.maven.plugins:maven-shade-plugin:3.2.4'
    testRuntimeOnly    'org.apache.maven.plugins:maven-shade-plugin:3.2.4'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

compileGroovy.enabled = false

signing {
    String signingKeyId = findProperty('signingKeyId')
    String signingKey = findProperty('signingKey')
    String signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri('https://s01.oss.sonatype.org/service/local/')
            username = project.ext.sonatypeUsername
            password = project.ext.sonatypePassword
        }
    }
}